/*
 * Starcoder - a server to read/write data from/to the stars, written in Go.
 * Copyright (C) 2018 InfoStellar, Inc.
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 *
 */

buildscript {
    repositories {
        jcenter()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
        maven {
            url 'http://dl.bintray.com/curioswitch/curiostack'
        }
        maven {
            url 'http://palantir.bintray.com/releases'
        }
        mavenLocal()
    }
    dependencies {
        classpath 'gradle.plugin.com.github.blindpirate:gogradle:0.8.0'
        classpath 'org.curioswitch.curiostack:gradle-curiostack-plugin:0.0.127'
    }
}

apply plugin: 'org.curioswitch.gradle-curiostack-plugin'
apply plugin: 'org.curioswitch.gradle-grpc-api-plugin'
apply plugin: 'com.github.blindpirate.gogradle'

golang {
    version = '1.10'
    packagePath = 'github.com/infostellarinc/starcoder'
}

def gopath = System.env['GOPATH'] ?: project.file('.gogradle/project_gopath').toString()

protobuf {
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                gofast {
                    option 'plugins=grpc'
                    option 'Mgoogle/protobuf/duration.proto=github.com/gogo/protobuf/types'
                    option 'Mgoogle/protobuf/timestamp.proto=github.com/gogo/protobuf/types'
                    // Output into root directory for gogradle
                    outputSubDir = '../../../../..'
                }
            }
        }
    }
    plugins {
        gofast {
            path = project.file("${gopath}/bin/protoc-gen-gofast")
        }
    }
}

sourceSets {
    main {
        proto {
            srcDir 'api'
        }
    }
}

task getProtocGoPlugin(type: com.github.blindpirate.gogradle.Go) {
    outputs.file project.file("${gopath}/bin/protoc-gen-gofast")
    go 'get -u github.com/gogo/protobuf/protoc-gen-gofast'
}

task deleteGeneratedFiles(type: Delete) {
    delete fileTree(project.projectDir) {
        include '**/*.pb.go'
        include '**/*.mock.go'
        include '**/rice-box.go'
    }
    delete '.gogradle'
    delete 'vendor'
}
tasks.clean.dependsOn deleteGeneratedFiles

gcloud {
    clusterBaseName = 'infostellar-starcoder'
    cloudRegion = 'asia-northeast1'
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.6'
    distributionType = 'ALL'
}

// protobuf-gradle-plugin always apply Java plugin, but we don't need it.
tasks.compileJava.enabled = false
tasks.compileTestJava.enabled = false

license {
    header file('LICENSE.header')
    mapping {
        proto = 'SLASHSTAR_STYLE'
    }
}


task depGet(type: com.github.blindpirate.gogradle.Go) {
    outputs.file project.file("${gopath}/bin/dep")
    go 'get -u github.com/golang/dep/cmd/dep'
    doLast {
        // Remove dep source code since it contains invalid symlinks that break gradle.
        project.delete project.file("${gopath}/src/github.com/golang/dep")
    }
}

task getRice(type: com.github.blindpirate.gogradle.Go) {
    go 'get -u github.com/GeertJohan/go.rice/...'
}

task useRice(type:Exec) {
    dependsOn getRice
    inputs.dir 'flowgraphs'
    outputs.file 'cmd/rice-box.go'

    workingDir project.file('cmd').toString()
    commandLine project.file('.gogradle/project_gopath/bin/rice').toString(), 'embed-go'
}

tasks.goBuild.dependsOn useRice

def buildManager = tasks.goBuild.buildManager

afterEvaluate {
    task depEnsure(type: Exec) {
        dependsOn depGet

        inputs.file 'Gopkg.toml'
        inputs.file 'Gopkg.lock'
        outputs.dir 'vendor'

        // TODO(anuraaga): Figure out why just setting workingDir doesn't work
        commandLine 'sh', '-c', "cd ${gopath}/src/github.com/infostellarinc/starcoder && " +
                "${project.file(gopath)}/bin/dep ensure -vendor-only"
        environment = [
                'DEPCACHEDIR': new File(gradle.gradleUserHomeDir, 'go/dep-cache').toString(),
                'GOPATH'     : gopath,
                'PATH': System.env['PATH']
        ]
        // workingDir "${gopath}/src/github.com/infostellarinc/starcoder"
    }

    task depUpdate(type: Exec) {
        dependsOn depGet

        inputs.file 'Gopkg.toml'
        outputs.file 'Gopkg.lock'

        // TODO(anuraaga): Figure out why just setting workingDir doesn't work
        commandLine 'sh', '-c', "cd ${gopath}/src/github.com/infostellarinc/starcoder && " +
                "${project.file(gopath)}/bin/dep ensure -no-vendor"
        environment = [
                'DEPCACHEDIR': new File(gradle.gradleUserHomeDir, 'go/dep-cache').toString(),
                'GOPATH'     : gopath,
                'PATH': System.env['PATH']
        ]
        // workingDir "${gopath}/src/github.com/infostellarinc/starcoder"
    }

    tasks.goVendor.dependsOn depEnsure
    tasks.depEnsure.dependsOn tasks.generateProto
    tasks.generateProto.dependsOn getProtocGoPlugin
    tasks.goBuild.dependsOn tasks.generateProto

    tasks.withType(com.github.blindpirate.gogradle.Go) {
        if (it.name.startsWith('build')) {
            dependsOn tasks.goVendor
        }
    }
}

// We use dep for dependency management, so disable all of gogradle's
tasks.installDependencies.enabled = false
tasks.resolveBuildDependencies.enabled = false
tasks.resolveTestDependencies.enabled = false

tasks.build.dependsOn tasks.goBuild

envs {
    conda 'miniconda2-gnuradio', 'Miniconda2-4.4.10', [
            'pybombs',
            'cheetah',
            'lxml',
            'mako',
            'requests',
            condaPackage('autoconf'),
            condaPackage('automake'),
            condaPackage('boost'),
            condaPackage('cmake'),
            condaPackage('gcc_linux-64'),
            condaPackage('gfortran_linux-64'),
            condaPackage('gxx_linux-64'),
            condaPackage('git'),
            condaPackage('gsl'),
            condaPackage('libtool'),
            condaPackage('make'),
            condaPackage('numpy'),
            condaPackage('swig'),
            condaPackage('wget'),
    ]
}

ext.gnuradioRoot = gradle.gradleUserHomeDir.toPath().resolve('curiostack/python/bootstrap/miniconda2-gnuradio')

def condaCommand(command) {
    return """
    . ${gnuradioRoot.resolve('etc/profile.d/conda.sh')} && \\
    conda activate && \\
    ${command}
    """
}

task setupPrefix(type: Exec) {
    onlyIf { !project.file("${gnuradioRoot}/lib/libgnuradio-blocks.so").exists() }

    dependsOn 'pythonSetup'
    executable 'bash'
    args '-c', condaCommand("""pybombs auto-config && \\
        pybombs -y recipes add-defaults && \\
        pybombs -y recipes add gr-starcoder ${project.file('gr-recipes')} && \\
        pybombs -y -v prefix init ${gnuradioRoot} -a gnuradio -R gnuradio-nogui
    """)
    // For some reason, cmake has trouble finding boost in certain situations.
    environment 'BOOST_ROOT', gnuradioRoot
}

task installGrStarcoder(type: Exec) {
    dependsOn setupPrefix
    executable 'bash'
    args '-c', condaCommand("""cmake ../../gr-starcoder -DCMAKE_BUILD_TYPE=RelWithDebInfo -DCMAKE_INSTALL_PREFIX=${gnuradioRoot} -Wno-dev && \\
        make &&
        make install
    """)

    workingDir project.file('build/starcoder-cmake')
    doFirst {
        project.file('build/starcoder-cmake').mkdirs()
    }
}
